:log info "blocklist import: started"

:local rbootver [/system routerboard get current-firmware]
:local version ([:tonum [:pick $rbootver 0 1]]*10 + [:tonum [:pick $rbootver 2 3]])
:local minversion 62

:log info "blocklist-REP: Detected ROS version $rbootver"
	
:if ($version >= $minversion ) do={
	:log info "blocklist import: Running with error handling"
} else={
	:log info "blocklist import: Running without error handling"
}

:log info "blocklist import: disabling info"
/system logging disable 0

:local duration [/system clock get time]

/import file-name=blocklists.rsc

/ip firewall address-list

:local prdkeys [find list=blocklists]
:global newips

:local countnew 0
:local countremoved 0
:local counttotal [:len $newips]
:local counterror 0

:if ($counttotal > 0 ) do={
	:foreach value in=$prdkeys do={
		:local keyindex [:find $newips [get $value address]]
		:if ($keyindex > 0) do={
			:set ($newips->($keyindex)) ""
		} else={
			remove $value
			:set countremoved ($countremoved+1)
		}
	}
	:if ($version >= $minversion ) do={
		:foreach value in=$newips do={
			:if ($value != "") do={
				:do { add list=blocklists address="$value" } on-error { :set counterror ($counterror+1) }
				:set countnew ($countnew+1)
			}
		}
	} else={
		:foreach value in=$newips do={
			:if ($value != "") do={
				add list=blocklists address="$value"
				:set countnew ($countnew+1)
			}
		}	
	}
}

:set newips
:set duration ([/system clock get time] - $duration)

/system logging enable 0
:log info "blocklist import: finished - enabled info"

:if ($version >= $minversion ) do={
	:log info "blocklist import: finished - $countremoved removed, $countnew new, $counterror errors, in $duration / $counttotal  total"
} else={
	:log info "blocklist import: finished - $countremoved removed, $countnew new, in $duration / $counttotal  total"
}
